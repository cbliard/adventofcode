# Day 8: Treetop Tree House

```elixir
defmodule Aoc do
  def day() do
    Regex.run(~r/day\d\d/, __ENV__.file)
    |> List.first()
  end

  def input(), do: input(day())

  def input(day) when is_integer(day) do
    day
    |> Integer.to_string()
    |> String.pad_leading(2, "0")
    |> then(&"day#{&1}")
    |> input()
  end

  def input(day) when is_binary(day) do
    File.read!(Path.join(dirname(), "#{day}.txt"))
  end

  defp dirname do
    Path.dirname(__ENV__.file)
  end
end
```

## Part 1

The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a  [tree house](https://en.wikipedia.org/wiki/Tree_house).

First, determine whether there is enough tree cover here to keep a tree house **hidden**. To do this, you need to count the number of trees that are **visible from outside the grid** when looking directly along a row or column.

The Elves have already launched a  [quadcopter](https://en.wikipedia.org/wiki/Quadcopter) to generate a map with the height of each tree (your puzzle input (_The Elves have already launched a quadcopter (your puzzle input)._)). For example:

```
30373
25512
65332
33549
35390
```

Each tree is represented as a single digit whose value is its height, where `0` is the shortest and `9` is the tallest.

A tree is **visible** if all of the other trees between it and an edge of the grid are **shorter** than it. Only consider trees in the same row or column; that is, only look up, down, left, or right from any given tree.

All of the trees around the edge of the grid are **visible** - since they are already on the edge, there are no trees to block the view. In this example, that only leaves the **interior nine trees** to consider:

* The top-left `5` is **visible** from the left and top. (It isn't visible from the right or bottom since other trees of height `5` are in the way.)
* The top-middle `5` is **visible** from the top and right.
* The top-right `1` is not visible from any direction; for it to be visible, there would need to only be trees of height **0** between it and an edge.
* The left-middle `5` is **visible**, but only from the right.
* The center `3` is not visible from any direction; for it to be visible, there would need to be only trees of at most height `2` between it and an edge.
* The right-middle `3` is **visible** from the right.
* In the bottom row, the middle `5` is **visible**, but the `3` and `4` are not.

With 16 trees visible on the edge and another 5 visible in the interior, a total of **`21`** trees are visible in this arrangement.

Consider your map; **how many trees are visible from outside the grid?**

## Part 2

Content with the amount of tree cover available, the Elves just need to know the best spot to build their tree house: they would like to be able to see a lot of **trees**.

To measure the viewing distance from a given tree, look up, down, left, and right from that tree; stop if you reach an edge or at the first tree that is the same height or taller than the tree under consideration. (If a tree is right on the edge, at least one of its viewing distances will be zero.)

The Elves don't care about distant trees taller than those found by the rules above; the proposed tree house has large  [eaves](https://en.wikipedia.org/wiki/Eaves) to keep it dry, so they wouldn't be able to see higher than the tree house anyway.

In the example above, consider the middle `5` in the second row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is not blocked; it can see **`1`** tree (of height `3`).
* Looking left, its view is blocked immediately; it can see only **`1`** tree (of height `5`, right next to it).
* Looking right, its view is not blocked; it can see **`2`** trees.
* Looking down, its view is blocked eventually; it can see **`2`** trees (one of height `3`, then the tree of height `5` that blocks its view).

A tree's **scenic score** is found by **multiplying together** its viewing distance in each of the four directions. For this tree, this is **`4`** (found by multiplying `1 * 1 * 2 * 2`).

However, you can do even better: consider the tree of height `5` in the middle of the fourth row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is blocked at **`2`** trees (by another tree with a height of `5`).
* Looking left, its view is not blocked; it can see **`2`** trees.
* Looking down, its view is also not blocked; it can see **`1`** tree.
* Looking right, its view is blocked at **`2`** trees (by a massive tree of height `9`).

This tree's scenic score is **`8`** (`2 * 2 * 1 * 2`); this is the ideal spot for the tree house.

Consider each tree on your map. **What is the highest scenic score possible for any tree?**

```elixir
example_input = """
30373
25512
65332
33549
35390
"""

example_solution_part1 = 21
example_solution_part2 = 8
```

```elixir
defmodule C do
  def to_data_model(trees) do
    left_right =
      for {row, y} <- Enum.with_index(trees),
          {h, x} <- Enum.with_index(row),
          {left, [_ | right]} = Enum.split(row, x),
          into: %{} do
        {{x, y}, %{pos: {x, y}, h: h, trees_lines_to_edge: [Enum.reverse(left), right]}}
      end

    for {col, x} <- Enum.with_index(transpose(trees)),
        {_, y} <- Enum.with_index(col),
        {up, [_ | down]} = Enum.split(col, y),
        reduce: left_right do
      acc ->
        Map.update(
          acc,
          {x, y},
          nil,
          fn data ->
            %{data | trees_lines_to_edge: [Enum.reverse(up), down | data.trees_lines_to_edge]}
          end
        )
    end
  end

  def transpose(grid) do
    grid
    |> List.zip()
    |> Enum.map(&Tuple.to_list/1)
  end

  def visible?(%{h: h, trees_lines_to_edge: trees_lines_to_edge}) do
    trees_lines_to_edge
    |> Enum.any?(fn
      trees_line -> Enum.all?(trees_line, fn tree -> tree < h end)
    end)
  end

  def scenic_score(%{h: h, trees_lines_to_edge: trees_lines_to_edge}) do
    trees_lines_to_edge
    |> Enum.map(fn trees_line -> viewing_distance(trees_line, h) end)
    |> Enum.reduce(fn d, acc -> d * acc end)
  end

  def viewing_distance([], _), do: 0
  def viewing_distance([tree | _rest], h) when tree >= h, do: 1
  def viewing_distance([_tree | rest], h), do: 1 + viewing_distance(rest, h)
end

trees =
  Aoc.input()
  # example_input
  |> String.split("\n", trim: true)
  |> Enum.map(fn line -> String.graphemes(line) |> Enum.map(&String.to_integer/1) end)
  |> C.to_data_model()
  |> Map.values()

trees
|> Enum.count(fn tree -> C.visible?(tree) end)
|> IO.inspect(label: "part 1")

trees
|> Enum.map(fn tree -> Map.put(tree, :scsc, C.scenic_score(tree)) end)
|> Enum.max_by(& &1.scsc)
|> then(& &1.scsc)
|> IO.inspect(label: "\npart 2")
```

```elixir
for n <- 1..10, do: 3

cond do
  true ->
    1

  :else ->
    0
end

[1, 2, 3, 4, 5]
|> Enum.slice(1, 42)

[1, 2, 3, 4, 5]
|> Enum.count(fn x -> x > 4 end)
```
