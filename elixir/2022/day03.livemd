# Day 3

## Day 3: Rucksack Reorganization

One Elf has the important job of loading all of the [rucksacks](https://en.wikipedia.org/wiki/Rucksack) with supplies for the jungle (_Where there's jungle, there's hijinxs._) journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.

Each rucksack has two large **compartments**. All items of a given type are meant to go into exactly one of the two compartments. The Elf that did the packing failed to follow this rule for exactly one item type per rucksack.

The Elves have made a list of all of the items currently in each rucksack (your puzzle input), but they need your help finding the errors. Every item type is identified by a single lowercase or uppercase letter (that is, `a` and `A` refer to different types of items).

The list of items for each rucksack is given as characters all on a single line. A given rucksack always has the same number of items in each of its two compartments, so the first half of the characters represent items in the first compartment, while the second half of the characters represent items in the second compartment.

For example, suppose you have the following list of contents from six rucksacks:

```
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
```

* The first rucksack contains the items `vJrwpWtwJgWrhcsFMMfFFhFp`, which means its first compartment contains the items `vJrwpWtwJgWr`, while the second compartment contains the items `hcsFMMfFFhFp`. The only item type that appears in both compartments is lowercase `**p**`.
* The second rucksack's compartments contain `jqHRNqRjqzjGDLGL` and `rsFMfFZSrLrFZsSL`. The only item type that appears in both compartments is uppercase `**L**`.
* The third rucksack's compartments contain `PmmdzqPrV` and `vPwwTWBwg`; the only common item type is uppercase `**P**`.
* The fourth rucksack's compartments only share item type `**v**`.
* The fifth rucksack's compartments only share item type `**t**`.
* The sixth rucksack's compartments only share item type `**s**`.

To help prioritize item rearrangement, every item type can be converted to a **priority**:

* Lowercase item types `a` through `z` have priorities 1 through 26.
* Uppercase item types `A` through `Z` have priorities 27 through 52.

In the above example, the priority of the item type that appears in both compartments of each rucksack is 16 (`p`), 38 (`L`), 42 (`P`), 22 (`v`), 20 (`t`), and 19 (`s`); the sum of these is `**157**`.

Find the item type that appears in both compartments of each rucksack. **What is the sum of the priorities of those item types?**

## Part 2

As you finish identifying the misplaced items, the Elves come to you with another issue.

For safety, the Elves are divided into groups of three. Every Elf carries a badge that identifies their group. For efficiency, within each group of three Elves, the badge is the **only item type carried by all three Elves**. That is, if a group's badge is item type `B`, then all three Elves will have item type `B` somewhere in their rucksack, and at most two of the Elves will be carrying any other item type.

The problem is that someone forgot to put this year's updated authenticity sticker on the badges. All of the badges need to be pulled out of the rucksacks so the new authenticity stickers can be attached.

Additionally, nobody wrote down which item type corresponds to each group's badges. The only way to tell which item type is the right one is by finding the one item type that is **common between all three Elves** in each group.

Every set of three lines in your list corresponds to a single group, but each group can have a different badge item type. So, in the above example, the first group's rucksacks are the first three lines:

```
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
```

And the second group's rucksacks are the next three lines:

```
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
```

In the first group, the only item type that appears in all three rucksacks is lowercase `r`; this must be their badges. In the second group, their badge item type must be `Z`.

Priorities for these items must still be found to organize the sticker attachment efforts: here, they are 18 (`r`) for the first group and 52 (`Z`) for the second group. The sum of these is `**70**`.

Find the item type that corresponds to the badges of each three-Elf group. **What is the sum of the priorities of those item types?**

```elixir
defmodule Aoc do
  def day() do
    Regex.run(~r/day\d\d/, __ENV__.file)
    |> List.first()
  end

  def input(), do: input(day())

  def input(day) when is_integer(day) do
    day
    |> Integer.to_string()
    |> String.pad_leading(2, "0")
    |> then(&"day#{&1}")
    |> input()
  end

  def input(day) when is_binary(day) do
    File.read!(Path.join(dirname(), "#{day}.txt"))
  end

  defp dirname do
    Path.dirname(__ENV__.file)
  end
end

example_input = """
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
"""

example_solution_part1 = 157
example_solution_part2 = 70
```

```elixir
defmodule Challenge do
  def part1(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&items/1)
    |> Enum.map(&half/1)
    |> Enum.map(&find_duplicate/1)
    |> Enum.map(&score/1)
    |> Enum.sum()
  end

  def part2(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&items/1)
    |> Enum.chunk_every(3)
    |> Enum.map(&find_duplicate/1)
    |> Enum.map(&score/1)
    |> Enum.sum()
  end

  def half(items) do
    {half1, half2} = Enum.split(items, div(length(items), 2))
    [half1, half2]
  end

  def find_duplicate(bags) do
    intersection(bags)
    |> List.first()
  end

  def intersection([items1, items2]) do
    items1 -- items1 -- items2
  end

  def intersection([items1 | rest]) do
    intersection([items1, intersection(rest)])
  end

  def items(s) do
    s
    |> String.graphemes()
  end

  def score(<<item>>) when item in ?a..?z, do: item - ?a + 1
  def score(<<item>>) when item in ?A..?Z, do: item - ?A + 27
end

example_answer_part1 =
  example_input
  |> Challenge.part1()
  |> IO.inspect(label: "example part2")

if example_answer_part1 == example_solution_part1 do
  IO.puts("FOUND PART 1")
end

example_answer_part2 =
  example_input
  |> Challenge.part2()
  |> IO.inspect(label: "example part2")

if example_answer_part2 == example_solution_part2 do
  IO.puts("FOUND PART 2")
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
IO.inspect(?a)

if example_answer_part1 == example_solution_part1 do
  Aoc.input()
  |> Challenge.part1()
  |> IO.inspect(label: "challenge part1")
else
  IO.puts("Bad part1 solution")
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
example_answer_part2 =
  example_input
  |> Challenge.part2()
  |> IO.inspect(label: "example part2")

if example_answer_part2 == example_solution_part2 do
  Aoc.input()
  |> Challenge.part2()
  |> IO.inspect(label: "challenge part2")
else
  IO.puts("Bad part2 solution")
end
```
